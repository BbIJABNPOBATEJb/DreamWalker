buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
    }
    dependencies {
        classpath("com.anatawa12.forge:ForgeGradle:1.2-1.0.+") {
            changing = true
        }
    }
}

apply plugin: "forge"

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = compileTestJava.options.encoding = "UTF-8"

version = "$version-$minecraft_version"
group = modGroup
archivesBaseName = modid

minecraft {
    version = forgeVersion
    mappings = "stable_12"
    runDir = "run"
}

repositories {
    mavenCentral()
    maven { url = "https://repo.spongepowered.org/maven" }
}

dependencies {
    implementation("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        exclude module: "launchwrapper"
        exclude module: 'guava'
    }
    annotationProcessor("org.spongepowered:mixin:0.7.11-SNAPSHOT")

    compileOnly("org.projectlombok:lombok:1.18.38")
    annotationProcessor("org.projectlombok:lombok:1.18.38")

    implementation 'org.xerial:sqlite-jdbc:3.42.0.0'
}

jar {
    manifest {
        attributes([
                "Implementation-Title"       : project.name,
                "Implementation-Version"     : project.version,
                "TweakClass"                 : "org.spongepowered.asm.launch.MixinTweaker",
                "MixinConfigs"               : "mixins.dreamwalker.json",
                "FMLCorePluginContainsFMLMod": "true",
                "ForceLoadAsMod"             : "true",
                "Implementation-Timestamp"   : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version,
                "mcversion": project.minecraft.version,
                "modid": modid,
                "modname": modname
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}


ext {
    mixinSrg = new File(project.buildDir, 'mixins/mixin.dreamwalker.srg')
    mixinRefMapName = "mixins.dreamwalker.refmap.json";
    mixinRefMap = new File(project.buildDir, "mixins/" + mixinRefMapName)
}

jar {
    from project.mixinRefMap
}

reobf {
    addExtraSrgFile project.mixinSrg
}

compileJava {
    options.compilerArgs += [
            '-Xlint:-processing',
            "-AoutSrgFile=${project.mixinSrg.canonicalPath}",
            "-AoutRefMapFile=${project.mixinRefMap.canonicalPath}",
            "-AreobfSrgFile=${project.file('build/srgs/mcp-srg.srg').canonicalPath}"
    ]
}


task copySrgs(type: Copy, dependsOn: 'genSrgs') {
    from plugins.getPlugin("forge").delayedFile('{SRG_DIR}')

    include '**/*.srg'
    into 'build/srgs'
}

compileJava.dependsOn copySrgs